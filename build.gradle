plugins {
	id 'java-library'
	id 'eclipse'
}

group = 'etri'
version = '24.08.09'

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
	
	withSourcesJar()
	withJavadocJar()
}
[compileJava,compileTestJava]*.options*.encoding = 'UTF-8'

ext {
	logback_version = '1.3.14'
	guava_version = '33.1.0-jre'
	rxjava3_version = '3.1.8'
//	picocli_version = '3.9.6'
	picocli_version = '4.6.2'
	
	cglib_version = '3.3.0'
	commons_io_version = '2.11.0'
	commons_text_version = '1.8'
	
	junit_version = '4.13.2'
	mockito_version = '3.12.4'
	hamcrest_version = '1.3'
	
	commons_beanutils_version = '1.9.4'
}

repositories {
    mavenCentral()
}

dependencies {
	// Logback with SLF4j
	implementation "ch.qos.logback:logback-classic:${logback_version}"
	
	api "com.google.guava:guava:${guava_version}"
	api "io.reactivex.rxjava3:rxjava:${rxjava3_version}"
	
	// command line parser
	implementation "info.picocli:picocli:${picocli_version}"
	
	implementation "commons-io:commons-io:${commons_io_version}"
	implementation "org.apache.commons:commons-text:${commons_text_version}"
	implementation "cglib:cglib:${cglib_version}"
	
	// Apache Commons BeanUtils
	implementation "commons-beanutils:commons-beanutils:${commons_beanutils_version}"
	
	testImplementation "junit:junit:${junit_version}"
	testImplementation "org.mockito:mockito-core:${mockito_version}"
}

task sourceJar(type: Jar) {
	from sourceSets.main.allSource
}

javadoc {
	options.encoding = 'UTF-8'
	options.links("https://docs.oracle.com/javase/8/docs/api/")
	options.links("https://google.github.io/guava/releases/23.0/api/docs/")
	options.links("http://reactivex.io/RxJava/javadoc/")
	
	options.addStringOption('Xdoclint:none', '-quiet')
}
//task javadocJar(type: Jar) {
//	from javadoc.destinationDir
//}

artifacts {
	archives sourceJar
	archives javadocJar
}

eclipse {
	classpath {
		file {
			withXml {
				def node = it.asNode()
				node.appendNode('classpathentry', [kind: 'output', path: 'build/classes'])
			}
			
			whenMerged { cp ->
				Map entryByPath = cp.entries.groupBy { entry -> entry.path }
				entryByPath.each { key, values ->
					if ( values.size() > 1 ) {
            			def entry = values.first()
            			if ( entry.kind == 'src' ) {
            				entry.includes = []
            				entry.excludes = []
            			}
            			int index = cp.entries.indexOf entry
            			cp.entries.removeAll values
            			cp.entries.add index, entry
					}
				}
				cp.entries.each { entry ->
					if ( entry.kind == 'src' && entry.hasProperty('output') ) {
						if ( entry.path.contains('/test/') ) {
							entry.output = 'build/test_classes'
						}
						else {
							entry.output = 'build/classes'
						}
					}
				}
			}
		}
	}
}
