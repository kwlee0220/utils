apply plugin: 'java-library'

group = 'etri'
version = '20.01.08'

sourceCompatibility = JavaVersion.VERSION_1_8
[compileJava,compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
	maven {
		url "https://jitpack.io"
	}
	maven {
		name = "io.vavr"
		url "https://oss.sonatype.org/content/repositories/snapshots"
	}
}

dependencies {
	api "org.slf4j:slf4j-log4j12:${slf4j_log4j_version}"
	api "com.google.code.findbugs:jsr305:${jsr305_version}"
	api "com.google.guava:guava:${guava_version}"
	api "commons-io:commons-io:${commons_io_version}"
	api "cglib:cglib:${cglib_version}"
	api "io.reactivex.rxjava2:rxjava:${rxjava2_version}"
	api "org.mvel:mvel2:${mvel2_version}"
	api "commons-cli:commons-cli:${commons_cli_version}"
	implementation "org.lz4:lz4-java:${lz4_java_version}"
	implementation "com.google.code.gson:gson:${gson_version}"
	api "org.apache.commons:commons-text:${commons_text_version}"
	
	testImplementation "junit:junit:${junit_version}"
	testImplementation "org.mockito:mockito-core:${mokito_version}"
	testImplementation "org.hamcrest:hamcrest-all:${hamcrest_version}"
}

task sourceJar(type: Jar) {
	classifier 'sources'
	from sourceSets.main.allSource
}

javadoc {
	options.encoding = 'UTF-8'
	options.links("https://docs.oracle.com/javase/8/docs/api/")
	options.links("https://google.github.io/guava/releases/23.0/api/docs/")
	options.links("http://reactivex.io/RxJava/javadoc/")
}
task javadocJar(type: Jar) {
	classifier 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourceJar
	archives javadocJar
}

apply plugin: 'maven-publish'
publishing {
	publications {
		main(MavenPublication) {
			components.java
		
			artifact sourceJar
			artifact javadocJar
		}
	}
}

apply plugin: 'eclipse'
eclipse {
	classpath {
		file {
			withXml {
				def node = it.asNode()
				node.appendNode('classpathentry', [kind: 'output', path: 'classes'])
			}
			
			whenMerged { cp ->
				Map entryByPath = cp.entries.groupBy { entry -> entry.path }
				entryByPath.each { key, values ->
					if ( values.size() > 1 ) {
            			def entry = values.first()
            			if ( entry.kind == 'src' ) {
            				entry.includes = []
            				entry.excludes = []
            			}
            			int index = cp.entries.indexOf entry
            			cp.entries.removeAll values
            			cp.entries.add index, entry
					}
				}
				cp.entries.each { entry ->
					if ( entry.kind == 'src' && entry.hasProperty('output') ) {
						entry.output = 'classes'
					}
				}
			}
		}
	}
}