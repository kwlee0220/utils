apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

buildscript {
	repositories {
		mavenLocal()
		jcenter()
	}
	dependencies {
		classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
	}
}

group = 'etri'
version = '20.02.12'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
[compileJava,compileTestJava]*.options*.encoding = 'UTF-8'

ext.log4j_version = '2.12.1'
ext.slf4j_log4j_version = '1.7.25'
ext.guava_version = '28.2-jre'
ext.commons_io_version = '2.6'
ext.cglib_version = '3.3.0'
ext.rxjava2_version = '2.2.0'
ext.commons_cli_version = '1.4'
ext.lz4_java_version = '1.7.1'
ext.gson_version = '2.8.6'
ext.commons_text_version = '1.8'
ext.mvel2_version = '2.4.5.Final'
ext.picocli_version = '3.9.6'

ext.junit_version = '4.12'
ext.mockito_version = '3.2.4'
ext.hamcrest_version = '1.3'

repositories {
    mavenCentral()
}

dependencies {
	api "org.slf4j:slf4j-log4j12:${slf4j_log4j_version}"
	api "com.google.guava:guava:${guava_version}"
	api "commons-io:commons-io:${commons_io_version}"
	api "cglib:cglib:${cglib_version}"
	api "io.reactivex.rxjava2:rxjava:${rxjava2_version}"
	api "commons-cli:commons-cli:${commons_cli_version}"
	implementation "com.google.code.gson:gson:${gson_version}"
	api "org.apache.commons:commons-text:${commons_text_version}"
	api "org.mvel:mvel2:${mvel2_version}"
	implementation "org.lz4:lz4-java:${lz4_java_version}"
	
	// command line parser
	api "info.picocli:picocli:${picocli_version}"
	
	testImplementation "junit:junit:${junit_version}"
	testImplementation "org.mockito:mockito-core:${mockito_version}"
	testImplementation "org.hamcrest:hamcrest-all:${hamcrest_version}"
}

task sourceJar(type: Jar) {
	classifier 'sources'
	from sourceSets.main.allSource
}

javadoc {
	options.encoding = 'UTF-8'
	options.links("https://docs.oracle.com/javase/8/docs/api/")
	options.links("https://google.github.io/guava/releases/23.0/api/docs/")
	options.links("http://reactivex.io/RxJava/javadoc/")
}
task javadocJar(type: Jar) {
	classifier 'javadoc'
	from javadoc.destinationDir
}

shadowJar {
	classifier 'all'
	
	mergeServiceFiles()
	
	exclude 'META-INF/*.SF'
	exclude 'META-INF/*.DSA'
	exclude 'META-INF/*.RSA'
}

artifacts {
	archives sourceJar
	archives javadocJar
	archives shadowJar
}

publishing {
	publications {
		common(MavenPublication) {
			from components.java
		
			artifact sourceJar
			artifact javadocJar
			artifact shadowJar
		}
	}
}

apply plugin: 'eclipse'
eclipse {
	classpath {
		file {
			withXml {
				def node = it.asNode()
				node.appendNode('classpathentry', [kind: 'output', path: 'classes'])
			}
			
			whenMerged { cp ->
				Map entryByPath = cp.entries.groupBy { entry -> entry.path }
				entryByPath.each { key, values ->
					if ( values.size() > 1 ) {
            			def entry = values.first()
            			if ( entry.kind == 'src' ) {
            				entry.includes = []
            				entry.excludes = []
            			}
            			int index = cp.entries.indexOf entry
            			cp.entries.removeAll values
            			cp.entries.add index, entry
					}
				}
				cp.entries.each { entry ->
					if ( entry.kind == 'src' && entry.hasProperty('output') ) {
						entry.output = 'classes'
					}
				}
			}
		}
	}
}
